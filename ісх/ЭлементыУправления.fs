namespace Бібліотека

open System
open Avalonia.Controls

[<Sealed>]
type ЭлементиКерування private () =
   static let mutable наКлік = Callback(ignore)
   static let mutable останняНажатаКнопка = ""
   static let элементиКерування = Словник<string,Avalonia.Controls.Control>()
   static let позиції = Словник<string,int * int>()
   /// Додає кнопку до графичного вікна
   static member ДодатиКнопку(позначка:string, x:int, y:int) =
      Моя.Апплікація.ВикликатиЗРезультатом(fun () ->
         let назва = "Button" + Guid.NewGuid().ToString()
         let кнопка = new Button(Content = позначка)
         кнопка.Click.Add(fun _ -> останняНажатаКнопка <- назва; наКлік.Invoke())
         элементиКерування.Add(назва,кнопка)
         позиції.Add(назва,(x,y))
         Моя.Апплікація.Полотно.ДодатиДітину(кнопка,float x,float y)
         назва
      )
   /// Добавляет текстовое поле ввода к графическому окну
   static member ДодатиТекстовеВікно(x,y) =
      Моя.Апплікація.ВикликатиЗРезультатом(fun () ->
          let назва = "ТекстовеВікно" + Guid.NewGuid().ToString()
          let элементКерування = new Avalonia.Controls.TextBox()
          элементКерування.Text <- "Бу!"
          элементиКерування.Add(назва,элементКерування)
          позиції.Add(назва,(x,y))
          Моя.Апплікація.Полотно.ДодатиДітину(элементКерування,float x,float y)
          назва
      )
   /// Встановлює розмір элемента керування
   static member ВстановитиРозмір(назваЕлемента, ширина:int, висота:int) =
      let элементКерування = элементиКерування.[назваЕлемента]
      let x, y = позиції.[назваЕлемента]
      Моя.Апплікація.Викликати( fun () ->
         Моя.Апплікація.Полотно.ВидалитиДітину(элементКерування)        
         элементКерування.Width <- float ширина
         элементКерування.Height <- float висота
         элементКерування.MinWidth <- float ширина
         элементКерування.MinHeight <- float висота
         Моя.Апплікація.Полотно.ДодатиДітину(элементКерування,float x,float y)
      )
   /// Встановлює текст у вказанному текстовому полі
   static member ВстановитиТекстТекстовогоВікна(назваЕлемента, текст) = 
      let элементКерування = элементиКерування.[назваЕлемента] :?> TextBox
      Моя.Апплікація.Викликати( fun () ->
         элементКерування.Text <- текст
         элементКерування.Focus()
      )
   /// Отримує поточний текст у вказанному текстовому полі
   static member ОтриматиТекстТекстовогоВікна(назваЕлемента) =
      let элементКерування = элементиКерування.[назваЕлемента] :?> TextBox
      элементКерування.Text
   static member КнопкаКликнута with set (зворотьнійВиклик:Callback) = наКлік <- зворотьнійВиклик
   static member ОстанняНажатаКнопка with get() = останняНажатаКнопка