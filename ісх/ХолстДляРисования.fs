namespace Бiблiотека

open Xwt
open Xwt.Drawing
open Рисовать

[<AllowNullLiteral>]
type internal ХолстДляРисования () =
   inherit Canvas ()
   let изображениеЧерепахи = Image.FromResource(typeof<ХолстДляРисования>, "ВеселШарп.Бiблiотека.черепаха.png")
   let рисунки = ResizeArray<ИнфоРисунка>()
   let черепаха =
      let ш,в = изображениеЧерепахи.Width, изображениеЧерепахи.Height
      {Рисунок=НамалюватиЗображення(ref изображениеЧерепахи,-ш/2.,-в/2.); Смещение=Point(); Непрозрачность=None; Видим=false; Врашение=None; Масштаб=None}
   let наФигуру имяФигуры ф =
      рисунки
      |> Seq.tryPick (function
         | { Рисунок=НарисоватьФигуру(name,_) } as info when name = имяФигуры -> Some info 
         | _ -> None
      )
      |> Option.iter ф   
   member холст.Черепаха = черепаха
   member холст.ОчиститиМалюнки() =
      рисунки.Clear()
      холст.QueueDraw()
   member холст.ДобавитьРисунок(рисунок) =
      { Рисунок=рисунок; Смещение=Point(); Непрозрачность=None; Видим=true; Врашение=None; Масштаб=None }
      |> рисунки.Add
      холст.QueueDraw()
   member холст.AddDrawingAt(рисунок, смещение:Point) =
      { Рисунок=рисунок; Смещение=смещение; Непрозрачность=None; Видим=true; Врашение=None; Масштаб=None }
      |> рисунки.Add
      холст.QueueDraw()
   member холст.ПереместитьФигуру(фигура, смещение:Point) =
      наФигуру фигура (fun инфо -> инфо.Смещение <- смещение; холст.QueueDraw())
   member холст.УстановитьНепрозрачностьФигуры(фигура, непрозрачность) =
      наФигуру фигура (fun инфо -> инфо.Непрозрачность <- Some непрозрачность; холст.QueueDraw())
   member холст.УстановитьВидимостьФигуры(фигура, видима) =
      наФигуру фигура (fun инфо -> инфо.Видим <- видима; холст.QueueDraw())
   member холст.УстановитьВращениеФигуры(фигура, угол) =
      наФигуру фигура (fun инфо -> инфо.Врашение <- Some(угол); холст.QueueDraw())
   member холст.УстановитьМасштабФигуры(фигура, масштабX, масштабY) =
      наФигуру фигура (fun инфо -> инфо.Масштаб <- Some(масштабX,масштабY); холст.QueueDraw())
   member холст.УдалитьФигуру(фигура) =
      рисунки |> Seq.tryFindIndex (function 
         | { ИнфоРисунка.Рисунок=НарисоватьФигуру(имяФигуры,_) } -> имяФигуры = фигура
         | _ -> false 
      )
      |> function Some индекс -> рисунки.RemoveAt(индекс) | None -> ()
   member холст.СделатьНедействительным() =
      холст.QueueDraw()
   override this.OnDraw(конт, прямоугольник) =
      base.OnDraw(конт, прямоугольник)      
      for рисунок in рисунки do 
         if рисунок.Видим then нарисовать конт рисунок
      if черепаха.Видим then нарисовать конт черепаха