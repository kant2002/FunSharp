открыть Библиотека

пусть гш = float ГрафическоеОкно.Ширина
пусть гв = float ГрафическоеОкно.Высота
пусть ракетка = Фигуры.ДобавитьПрямоугольник(120, 12)
пусть шарик = Фигуры.ДобавитьЭллипс(16, 16)
пусть изменяемый счет = 0

пусть НаДвижениеМыши () =
  пусть ракеткаX = ГрафическоеОкно.МышьX
  Фигуры.Переместить(ракетка, ракеткаX - 60.0, гв - 12.0)

пусть НапечататьСчет () =
  // Clear the score first and тогда draw the real score text
  ГрафическоеОкно.ЦветКисти <- Цвета.White
  ГрафическоеОкно.ЗаполнитьПрямоугольник(10, 10, 200, 20)
  ГрафическоеОкно.ЦветКисти <- Цвета.Black
  ГрафическоеОкно.НарисоватьТекст(10, 10, "Score: " + счет.ToString())

ГрафическоеОкно.РазмерШрифта <- 14.0
ГрафическоеОкно.МышьПеремещена <- Callback(НаДвижениеМыши)

НапечататьСчет()
Звук.ИгратьЗвонок (*AndWait*) ()

пусть изменяемый x = 0.0
пусть изменяемый y = 0.0
пусть изменяемый дельтаX = 1.0
пусть изменяемый дельтаY = 2.0

пока (y < гв) сделать
  x <- x + дельтаX
  y <- y + дельтаY
  
  если (x >= гш - 16.0 || x <= 0.0) тогда
    дельтаX <- -дельтаX 
  если (y <= 0.0) тогда
    дельтаY <- -дельтаY
 
  пусть ракX = Фигуры.ПолучитьЛево(ракетка)
  если (y = гв - 28.0 && x >= ракX && x <= ракX + 120.0) тогда
    //Звук.ИгратьЩелчек()
    счет <- счет + 10
    НапечататьСчет()
    дельтаY <- -дельтаY  

  Фигуры.Переместить(шарик, x, y)
  Программа.Задержка(15)
  
ГрафическоеОкно.ПоказатьСообщение("Ваш счет: " + счет.ToString(), "Ракетка")
